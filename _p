#compdef p

_p() {
  local -a subcommands parent_dirs repos trees
  parent_dirs=("$HOME/code" "$HOME/hc" "$HOME/scripts")
  subcommands=('new:create new worktree' 'cd:navigate to repo or worktree' 'done:remove current worktree' 'goto:fuzzy-find repo or worktree' 'g:fuzzy-find repo or worktree')

  case $CURRENT in
    2)
      _describe -t commands 'command' subcommands
      ;;

    3)
      case $words[2] in
        cd)
          for parent in $parent_dirs; do
            [[ -d "$parent" ]] || continue
            for repo in "$parent"/*(/N); do
              local name=${repo:t}
              [[ "$name" == *.* ]] && continue
              repos+=("$name")
            done
          done
          _describe -t repositories 'repository' repos
          ;;
      esac
      ;;

    4)
      case $words[2] in
        cd)
          local repo_name=$words[3]
          local repo_parent

          for parent in $parent_dirs; do
            if [[ -d "$parent/$repo_name" ]]; then
              repo_parent=$parent
              break
            fi
          done

          if [[ -n $repo_parent ]]; then
            for worktree in "$repo_parent/$repo_name".*(/N); do
              local tree_name=${worktree:t}
              tree_name=${tree_name#$repo_name.}
              trees+=("$tree_name")
            done
            [[ ${#trees} -gt 0 ]] && _describe -t worktrees 'worktree' trees
          fi
          ;;
      esac
      ;;
  esac
}

_p "$@"
