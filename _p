#compdef p

# Source worktree functions for helpers
source "$HOME/scripts/git/worktree.zsh"

_p() {
  local -a subcommands repos trees branches
  subcommands=(
    'new:create new worktree'
    'n:create new worktree'
    'get:checkout existing branch as worktree'
    'e:checkout existing branch as worktree'
    'cd:navigate to repo or worktree'
    'c:navigate to repo or worktree'
    'done:remove current worktree'
    'd:remove current worktree'
    'goto:fuzzy-find repo or worktree'
    'g:fuzzy-find repo or worktree'
    'use:fuzzy-find and switch to branch'
    'u:fuzzy-find and switch to branch'
  )

  case $CURRENT in
    2)
      _describe -t commands 'command' subcommands
      ;;

    3)
      case $words[2] in
        get|e)
          branches=(${(f)"$(_wt_list_all_branches)"})
          _describe -t branches 'branch' branches
          ;;
        cd|c)
          repos=(${(f)"$(_wt_list_all_repos)"})
          _describe -t repositories 'repository' repos
          ;;
      esac
      ;;

    4)
      case $words[2] in
        cd|c)
          local repo_name=$words[3]
          trees=(${(f)"$(_wt_list_worktrees "$repo_name" 2>/dev/null)"})
          [[ ${#trees} -gt 0 ]] && _describe -t worktrees 'worktree' trees
          ;;
      esac
      ;;
  esac
}

_p "$@"
